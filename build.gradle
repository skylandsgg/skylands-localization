plugins {
    id 'java'
    id 'maven-publish'
}

def id = "SkylandsLocalization"
def group = 'gg.skylands.localization'
def ver = '1.0.2'

repositories {
    mavenCentral()

    maven {
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }

    maven {
        url = uri("https://repo.negative.games/repository/skylands/")
    }
}

dependencies {

    compileOnly("net.kyori:adventure-api:4.19.0")
    compileOnly("net.kyori:adventure-text-minimessage:4.19.0")

    // https://mvnrepository.com/artifact/redis.clients/jedis
    compileOnly('redis.clients:jedis:5.2.0')

    compileOnly("gg.skylands.protocol:skylands-protocol:1.1.1")

    // https://mvnrepository.com/artifact/com.google.guava/guava
    compileOnly("com.google.guava:guava:33.4.0-jre")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.32")
    annotationProcessor("org.projectlombok:lombok:1.18.32")

    // Jetbrains Annotations
    compileOnly("org.jetbrains:annotations:22.0.0")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

// Allow to build sources jar
tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// Artifact Information
artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "nexus"
            url = System.getenv("REPO_URL") ?: "https://repo.negative.games/repository/skylands/"
            credentials {
                username = project.findProperty("nexusUsername") ?: ""
                password = project.findProperty("nexusPassword") ?: ""
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'gg.skylands.localization'
            artifactId = 'skylands-localization'
            version = ver

            artifact(tasks.sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}
